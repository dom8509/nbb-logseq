{:paths ["script"]
 :deps
 {nbb.build/nbb.build
  {:git/url "https://github.com/babashka/nbb"
   :git/sha "e2e5c181a1e029f47fc47d3c688cd79ec86e753e"
   :deps/root "build"}
  #_{:local/root "../nbb/build"}
  nbb/nbb
  {:git/url "https://github.com/babashka/nbb"
   :git/sha "e2e5c181a1e029f47fc47d3c688cd79ec86e753e"
   :git/tag "v0.3.10"}
  #_{:local/root "../nbb"}
  datascript/deps
  {:git/url "https://github.com/babashka/nbb-features"
   :git/sha "7af8569a7f932af2cde5f8677133915540ab0c49"
   :deps/root "features/datascript"}
  datascript-transit/deps
  {:git/url "https://github.com/babashka/nbb-features"
   :git/sha "7af8569a7f932af2cde5f8677133915540ab0c49"
   :deps/root "features/datascript-transit"}
  linked/deps
  {:git/url "https://github.com/babashka/nbb-features"
   :git/sha "7af8569a7f932af2cde5f8677133915540ab0c49"
   :deps/root "features/linked"}
  cljs-time/deps {:local/root "features/cljs-time"}}

 :tasks
 {:requires ([babashka.fs :as fs]
             [nbb.build :as build]
             [clojure.string :as str])

  clean (fs/delete-tree "lib")

  release {:depends [clean]
           :doc "Compiles release build."
           :task (build/release *command-line-args*)}

  test nbb-feature-tests/main

  update-nbb
  {:desc "Update to latest nbb tag"
   :extra-deps {borkdude/rewrite-edn {:mvn/version "0.2.0"}}
   :requires ([borkdude.rewrite-edn :as r])
   :task
   (let [update-bb-dep
         (fn [dep dep-map]
           (let [nodes (-> "bb.edn" slurp r/parse-string)]
             (spit "bb.edn"
                   (str (reduce (fn [acc [k v]]
                                  (r/assoc-in acc [:deps dep k] v))
                                nodes
                                dep-map)))))
         [_ sha tag]
         (->> (shell {:out :string} "git ls-remote --heads --tags https://github.com/babashka/nbb.git")
              :out
              str/split-lines
              last
              (re-matches (re-pattern "(\\S+)\\trefs/tags/([v.0-9]+).*")))]
     (assert (and sha tag) "Sha and tag must exist to continue")
     (update-bb-dep 'nbb/nbb {:git/sha sha :git/tag tag})
     (update-bb-dep 'nbb.build/nbb.build {:git/sha sha})
     (println "Update nbb to" tag)
     (shell "git commit -m" (str "Update nbb to " tag) "."))}

  ;; Publish tasks copied from nbb
  current-tag (->> (shell {:out :string} "git describe")
                   :out
                   str/trim
                   (re-matches (re-pattern "^v\\d+\\.\\d+\\.\\d+")))

  current-branch (->> (shell {:out :string} "git rev-parse --abbrev-ref HEAD")
                      :out
                      str/trim)

  ci:is-release {:depends [current-tag current-branch]
                 :task (and current-tag (= "main" current-branch))}

  ci:publish {:doc "Publishes release build to npm"
              :depends [ci:is-release]
              :task
              (if ci:is-release
                (do
                  (println "Releasing")
                  (run 'release)
                  (shell "npm publish --access public"))
                (println "Skipping release."))}}}
